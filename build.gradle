buildscript {
    repositories {
        jcenter()
        maven { url "https://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle-Phanta:2.3.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0.41"
group = "xyz.phanta.tconevo"
archivesBaseName = "tconevo-1.12.2"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    useDepAts = true
}

repositories {
    mavenLocal()
    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com/'
    }
    maven {
        name 'DVS1 Repo'
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        name "OpenComputers"
        url "https://maven.cil.li"
    }
    maven {
        name "CoFH Maven"
        url "https://maven.covers1624.net"
    }
    maven {
        name "Thiakil Maven"
        url "https://maven.thiakil.com/"
    }
    maven {
        name "BlameJared Maven"
        url "https://maven.blamejared.com/"
    }
    maven {
        name "Bluexin Maven"
        url "https://maven.bluexin.be/repository/snapshots/"
    }
    maven {
        name "tterrag Maven"
        url "https://maven.tterrag.com/"
    }
    maven {
        name "sfPlayer1 Maven"
        url "https://maven.ic2.player.to/"
    }
    maven {
        name "Darkhax Maven"
        url "https://maven.mcmoddev.com"
    }
}

dependencies { // THE BEAST
    compile 'io.github.phantamanta44.libnine:libnine-1.12.2:1.2.0'
    deobfCompile 'mezz.jei:jei_1.12.2:4.15.0.293'
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.56"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.13.0.184"
    deobfCompile "curse.maven:constructs-armory-287683:2882794" // 1.2.5.4
    deobfCompile 'com.azanor.baubles:Baubles:1.12-1.5.2'
    deobfCompile "curse.maven:codechicken-lib-1-8-242818:2779848" // 3.2.3.358
    deobfCompile "cofh:RedstoneFlux:1.12-2.1.0.7:universal"
    deobfCompile "curse.maven:brandons-core-231382:3051539" // 2.4.19.214
    deobfCompile "curse.maven:draconic-evolution-223565:3051542" // 2.3.27.353
    deobfCompile "vazkii.botania:Botania:r1.10-363.148"
    deobfCompile "com.teamwizardry.librarianlib:librarianlib-1.12.2:4.19.1"
    deobfCompile "curse.maven:natural-pledge-247704:2740703" // r3.1.2
    deobfCompile "cofh:CoFHCore:1.12.2-4.6.3.27:universal"
    deobfCompile "cofh:ThermalFoundation:1.12.2-2.6.3.27:universal"
    deobfCompile "cofh:ThermalExpansion:1.12.2-5.5.4.43:universal"
    deobfCompile "curse.maven:industrial-foregoing-266515:2745321" // 1.12.13-237
    deobfCompile 'appeng:appliedenergistics2:rv6-stable-7'
    deobfCompile "curse.maven:mekanism-268560:2835175" // 9.8.3.390
    deobfCompile "curse.maven:mekanism-generators-268566:2835177" // 9.8.3.390
    deobfCompile "curse.maven:actually-additions-228404:2844115" // r151-2
    deobfCompile "curse.maven:thaumcraft-223628:2629023" // 6.1.BETA26
    deobfCompile "curse.maven:astral-sorcery-241721:2971187" // 1.10.24
    deobfCompile "curse.maven:blood-magic-224791:2822288" // 2.4.3-105
    deobfCompile "com.progwml6.natura:natura:1.12.2-4.3.2.69"
    deobfCompile "curse.maven:projecte-226410:2702991" // 1.4.1
    deobfCompile "curse.maven:valkyriecompat-289532:2691540" // 2.0.20.1
    deobfCompile "curse.maven:valkyrielib-245480:2691542" // 2.0.20.1
    deobfCompile "curse.maven:environmental-tech-245453:2691536" // 2.0.20.1
    compile "net.industrial-craft:industrialcraft-2:2.8.170-ex112:api"
    deobfCompile "curse.maven:advanced-solar-panels-252714:2652182" // 4.2.1
    deobfCompile "curse.maven:natural-absorption-224296:2678478" // 1.0.0
    deobfCompile "curse.maven:redstone-repository-revolved-300750:2695517" // 1.4.0-dev
    deobfCompile "curse.maven:solar-flux-reborn-246974:3050838" // 12.4.11
    deobfCompile "com.enderio.core:EnderCore:1.12.2-0.5.76"
    deobfCompile("com.enderio:EnderIO:1.12.2-5.2.59") { exclude group: "deobf.com.enderio.core" }
    deobfCompile "net.sengir.forestry:forestry_1.12.2:5.8.2.387"
    deobfCompile("morph.avaritia:Avaritia:1.12.2-3.3.0.33:universal") { exclude group: "codechicken" }
    compile "net.darkhax.bookshelf:Bookshelf-1.12.2:2.3.590"
    compile "net.darkhax.gamestages:GameStages-1.12.2:2.0.123"
    deobfCompile 'curse.maven:tinkers-tool-leveling-250957:2630860' // 1.1.0
}

// when forgegradle deobfs dependency jars, it'll try to cheat and grab the sources jar and remap *that* to avoid decompiling
// however, since we use IDE builds instead of delegating to maven, this means pre-deobf'd sources jars that are built
// on different deobf mappings versions will completely F up the build with class/member resolution errors
// this is a dumb workaround for that: we just delete the tasks that do the cheating and thus force forge to deobf built jars
Set SOURCES_BLACKLIST = ['CoFHCore-1.12.2']
afterEvaluate {
    tasks.deobfCompileDummyTask.dependsOn.removeIf {
        if (it.hasProperty('inJar')) {
            def name = it.inJar.name
            if (name.endsWith('-sources.jar')) {
                def artifact = name.substring(0, name.substring(0, name.length() - 12).lastIndexOf('-'))
                print "Found sources jar: $artifact"
                if (artifact in SOURCES_BLACKLIST) {
                    println " (BLACKLISTED)"
                    return true
                } else {
                    println()
                }
            }
        }
        return false
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'tconevo_at.cfg'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLCorePlugin': 'xyz.phanta.tconevo.coremod.TconEvoCoreMod'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    // don't copy asset generation scripts
    exclude '**/*.js'
    exclude '**/*.py'
    exclude '**/*.sh'
}
